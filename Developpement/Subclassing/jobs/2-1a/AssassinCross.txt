//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena, corrigé par Fern
//===== eAthena Script ======================================= 
//= Advance Class jobchanger after kRO Normals.
//===== By: ==================================================
//= Nana
//= finished, optimized and tested by Lupus
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0 +
//===== Description: ========================================= 
//= Advance Class jobchanger after kRO Normals.
//===== Additional Comments: ================================= 
//= v1.1 Made all into functions, additional checks, etc. [Lupus]
//= v1.2 Made numbers into constants. [Vicious]
//= 1.3 Now saves/restores all the quest skills [Lupus]
//============================================================ 

valkyrie,44,58,5	script	Assassin Cross#Valkyrie	725,{
	if (SUBCLASSING == 1  && Class != 4018 && Class != 4013)
	{
		mes "[Assassin Cross]";
		mes "Tu as choisi le métier d'Assassin Cross en tant que classe Vane?";
		mes "Es-tu bien sûr de ton choix? Il n'y a pas de machine arrière.";
		switch(select("Oui","Non", "Je ne sais pas encore. Quels skills sont disponibles?"))
		{
			case 1:
				mes "Très bien. Qu'il en soit ainsi.";
				set SUBCLASSING, 4013; 
				getitem 21098, 1;
				break;
			case 2:
				mes "Tu as raison. Réfléchis bien.";
				break;
			case 3:
				mes "Tu veux en savoir plus? Bien sûr!";
				mes "C'est logique. Vu le poids de ta décision sur ton futur.";
				mes "En tant qu'Assassin Cross, tu peux accéder aux skills suivants:";
				mes "Soul Breaker, Meteor Assault, Cloaking, Enchant Poison, Poison React, Venom Dust et Venom Splasher.";
				mes "Voilà, tu en sais un peu plus. Réfléchis bien.";
				break;
		} 
		close;
	}
	else if (ADVJOB == 0 || Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Assassin Cross]";
			mes "Félicitations...";
			next;
			mes "[Assassin Cross]";
			mes "...";
			next;
			mes "[Assassin Cross]";
			mes "...";
			mes "......";
			next;
			mes "[Assassin Cross]";
			mes "...";
			mes "......";
			mes "Honneur aux Guerriers.";
			close;
		}
		mes "[Assassin Cross]";
		mes "Nous sommes les guerriers du désert. Personne ne nous prend de haut. Personne...";
		close;
	}
	else if (ADVJOB == Job_Assassin_Cross && Class == Job_Thief_High && JobLevel > 39) {
		mes "[Assassin Cross]";
		mes "Le temps est venu.";
		mes "Le monde a besoin de vous...";
		mes "Plus que jamais.";
		next;
		mes "[Assassin Cross]";
		mes "Je vous demande de continuer à vivre dans l'ombre, mais vous aurez besoin de plus de puissance, de changer d'apparence.";
		next;
		mes "[Assassin Cross]";
		mes "Souhaitez-vous devenir Assassin Cross?";
		next;
		if (select("Non.:Oui.") == 1) {
			mes "[Assassin Cross]";
			mes "Revenez quand vous vous sentirez prêt.";
			next;
			mes "[Assassin Cross]";
			mes "Honneur aux Guerriers!";
			close;
		}
		if (SkillPoint) {
			mes "[Assassin Cross]";
			mes "Vous n'avez pas terminé votre apprentissage.";
			close;
		}
		jobchange Job_Assassin_Cross;
		set ADVJOB,0;
		mes "[Assassin Cross]";
		mes "Félicitations.";
		mes "En tant qu'Assassin Cross, j'espère que vous combattrez pour un futur meilleur.";
		close;
	}

	mes "[Assassin Cross]";
	mes "Bienvenue au Walhalla, le Hall des Honneurs.";
	next;
	mes "[Assassin Cross]";
	mes "Faites comme chez vous.";
	mes "Honneur aux Guerriers!";
	close;
}
