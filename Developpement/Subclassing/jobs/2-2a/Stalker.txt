//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Advance Class jobchanger after kRO Normals.
//===== By: ==================================================
//= Nana
//= finished, optimized and tested by Lupus
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0 +
//===== Description: ========================================= 
//= Advance Class jobchanger after kRO Normals.
//===== Additional Comments: ================================= 
//= v1.1 Made all into functions, additional checks, etc. [Lupus]
//= v1.2 Made numbers into constants. [Vicious]
//= 1.3 Now saves/restores all the quest skills [Lupus]
//============================================================ 

valkyrie,53,58,3	script	Rôdeuse::Rôdeur#Valkyrie	747,{
	if (SUBCLASSING == 1 && Class != 4018 && Class != 4013)
	{
		mes "[Rôdeuse]";
		mes "Tu as choisi le métier de Stalker en tant que classe Vane?";
		mes "Es-tu bien sûr de ton choix? Il n'y a pas de machine arrière.";
		switch(select("Oui","Non","Je ne sais pas, tu peux m'en dire plus?"))
		{
			case 1:
				mes "Très bien. Qu'il en soit ainsi.";
				set SUBCLASSING, 4018; 
				getitem 21098, 1;
				break;
			case 2:
				mes "Tu as raison. Réfléchis bien.";
				break;
			case 3:
				mes "Tu veux en savoir plus? Bien sûr!";
				mes "C'est logique. Vu le poids de ta décision sur ton futur.";
				mes "En tant que Stalker, tu peux accéder aux skills suivants:";
				mes "Chase Walk, Reject Sword, Full Strip, Vulture's Eye, Double Straffing.";
				mes "Back Stab, Tunnel Drive, Raid et Intimitade.";
				mes "Voilà, tu en sais un peu plus. Réfléchis bien.";
				break;
		} 
		close;
	}
	else if (ADVJOB == 0 || Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Rôdeuse]";
			mes "Félicitations.";
			mes "Honneur aux Guerriers!";
			close;
		}
		mes "[Rôdeuse]";
		mes "Hé...";
		mes "Ce n'est pas facile d'être à la fois un héros, tout en devant resté caché dans l'ombre, incompris...";
		next;
		mes "[Rôdeuse]";
		mes "Mais quand les temps s'assombrissent, vos amis savent qu'ils peuvent compter sur vous. Ils ont besoin de vous, et vous avez besoin d'eux.";
		close;
	}
	else if (ADVJOB == Job_Stalker && Class == Job_Thief_High && JobLevel > 39) {
		mes "[Rôdeuse]";
		mes "Le monde a besoin de héros qui marchent sur la ligne entre l'ordre et le chaos.";
		next;
		mes "[Rôdeuse]";
		mes "Souhaitez-vous devenir Rôdeur?";
		next;
		if (select("Non.:Oui.") == 1) {
			mes "[Rôdeuse]";
			mes "Revenez quand vous vous sentirez prêt.";
			next;
			mes "[Rôdeuse]";
			mes "Honneur aux Guerriers!";
			close;
		}
		if (SkillPoint) {
			mes "[Rôdeuse]";
			mes "Vous n'avez pas terminé votre apprentissage.";
			close;
		}
		jobchange Job_Stalker;
		set ADVJOB,0;
		mes "[Rôdeuse]";
		mes "Félicitations!";
		mes "En tant que Rôdeur, j'espère que vous poignarderez les bonnes personnes dans le dos.";
		close;
	}

	mes "[Rôdeuse]";
	mes "Bienvenue au Walhalla, le Hall des Honneurs.";
	next;
	mes "[Rôdeuse]";
	mes "Faites comme chez vous.";
	mes "Honneur aux Guerriers!";
	close;
}
