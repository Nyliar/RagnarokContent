//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena, corrigé par Fern
//===== eAthena Script ======================================= 
//= Advance Class jobchanger after kRO Normals.
//===== By: ==================================================
//= Nana
//= finished, optimized and tested by Lupus
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0 +
//===== Description: ========================================= 
//= Advance Class jobchanger after kRO Normals.
//===== Additional Comments: ================================= 
//= v1.1 Made all into functions, additional checks, etc. [Lupus]
//= v1.2 Made numbers into constants. [Vicious]
//= 1.3 Now saves/restores all the quest skills [Lupus]
//============================================================ 

valkyrie,53,50,3	script	Biochimiste::Créateur#Valkyrie	122,{
	if (ADVJOB == 0 || Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Biochimiste]";
			mes "Félicitations.";
			mes "Honneur aux Guerriers!";
			close;
		}
		mes "[Biochimiste]";
		mes "Il est étrange que quelqu'un comme moi vive dans un endroit comme celui-ci. Mais vous savez, même les scientifiques peuvent devenir des héros.";
		next;
		mes "[Biochimiste]";
		mes "L'important, ce n'est pas tant les moyens utilisés que le résultat final, qui m'a permis de gagner une place au Walhalla...";
		close;
	}
	else if (ADVJOB == Job_Creator && Class == Job_Merchant_High && JobLevel > 39) {
		mes "[Biochimiste]";
		mes "Oui...";
		mes "L'heure est bientôt arrivée...";
		mes "Nous avons besoin de génies tels que vous dans Rune-Midgard.";
		next;
		mes "[Biochimiste]";
		mes "Souhaitez-vous devenir Biochimiste?";
		next;
		if (select("Non.:Oui.") == 1) {
			mes "[Biochimiste]";
			mes "Revenez quand vous vous sentirez prêt.";
			next;
			mes "[Biochimiste]";
			mes "Honneur aux Guerriers!";
			close;
		}
		if (SkillPoint) {
			mes "[Biochimiste]";
			mes "Vous n'avez pas terminé votre apprentissage.";
			close;
		}
		jobchange Job_Creator;
		set ADVJOB,0;
		mes "[Biochimiste]";
		mes "Félicitations!";
		mes "En tant que Biochimiste, j'espère que vous utiliserez vos vastes connaissances pour défendre des causes justes.";
		close;
	}
	else {
		if (Class == Job_Creator && MISC_QUEST & 64 && getskilllv("AM_BIOETHICS") == 0) {
			mes "[Biochimiste]";
			mes "Ah, vous êtes venu pour retrouver certains de vos souvenirs? Souhaitez-vous vous remémorer les secrets dont vous aviez percé les mystères...?";
			next;
			if (select("Oui:Non") == 1) {
				mes "[Biochimiste]";
				mes "Fermez vos yeux et faites le vide dans votre esprit. Nous allons retourner dans votre passé pour rassembler les fragments de votre mémoire perdue.";
				next;
				mes "[Biochimiste]";
				mes "Lorsque vous ouvrirez vos yeux, vous vous souviendrez clairement du secret de la vie. Vous vous souviendrez également du poids et de la responsabilité d'utiliser ces secrets pour des causes justes...";
				next;
				skill 238,1,0;
				mes "[Biochimiste]";
				mes "Ouvrez vos yeux...";
				mes "Vous vous souvenez maintenant comment créer la vie artificielle. Je vous demande simplement de traiter toutes vos créations avec respect.";
				close;
			}
			mes "[Biochimiste]";
			mes "Si vous souhaitez retrouver certains de vos souvenirs perdus, revenez me voir. Le secret de la création de la vie n'est pas une chose futile...";
			close;
		}
		mes "[Biochimiste]";
		mes "Bienvenue au Walhalla, le Hall des Honneurs.";
		next;
		mes "[Biochimiste]";
		mes "Faites comme chez vous.";
		mes "Honneur aux Guerriers!";
		close;
	}
}

