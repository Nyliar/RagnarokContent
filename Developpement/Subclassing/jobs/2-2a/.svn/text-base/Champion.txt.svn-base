//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena, corrigé par Fern
//===== eAthena Script ======================================= 
//= Advance Class jobchanger after kRO Normals.
//===== By: ==================================================
//= Nana
//= finished, optimized and tested by Lupus
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0 +
//===== Description: ========================================= 
//= Advance Class jobchanger after kRO Normals.
//===== Additional Comments: ================================= 
//= v1.1 Made all into functions, additional checks, etc. [Lupus]
//= v1.2 Made numbers into constants. [Vicious]
//= 1.3 Now saves/restores all the quest skills [Lupus]
//============================================================ 

valkyrie,53,42,3	script	Champion#Valkyrie	52,{
	if (ADVJOB == 0 || Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Champion]";
			mes "Félicitations.";
			mes "Honneur aux Guerriers!";
			close;
		}
		mes "[Champion]";
		mes "Habileté.";
		mes "Rapidité.";
		mes "Force.";
		mes "Agilité.";
		next;
		mes "[Champion]";
		mes "Un Champion peut bénéficier de toutes ces caractéristiques. Mais seul un être exceptionnel peut les maîtriser toutes en une vie...";
		close;
	}
	else if (ADVJOB == Job_Champion && Class == Job_Acolyte_High && JobLevel > 39) {
		mes "[Champion]";
		mes "C'est l'heure.";
		mes "L'heure pour les grands héros de se soulever contre les forces du mal qui envahissent le monde de Rune-Midgard!";
		next;
		mes "[Champion]";
		mes "Souhaitez-vous devenir Champion?";
		next;
		if (select("Non.:Oui.") == 1) {
			mes "[Champion]";
			mes "Revenez quand vous vous sentirez prêt.";
			next;
			mes "[Champion]";
			mes "Honneur aux Guerriers!";
			close;
		}
		if (SkillPoint) {
			mes "[Champion]";
			mes "Vous n'avez pas terminé votre apprentissage.";
			close;
		}
		jobchange Job_Champion;
		set ADVJOB,0;
		mes "[Champion]";
		mes "Félicitations!";
		mes "En tant que Champion, j'espère que vous apporterez de la lumière au monde grâce à la force de vos poings.";
		close;
	}
	mes "[Champion]";
	mes "Bienvenue au Walhalla, le Hall des Honneurs.";
	next;
	mes "[Champion]";
	mes "Faites comme chez vous.";
	mes "Honneur aux Guerriers!";
	close;
}
