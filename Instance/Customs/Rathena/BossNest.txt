//===== Alayne Scripts =======================================
//= Boss Nest Instance
//===== By: ================================================== 
//= Alayne
//===== Current Version: ===================================== 
//=
//= 1.0 - Creation
//= 1.1 - Structure modification, pattern apply by Emistry
//= 1.2 - Logical mods over stars for hercules support
//= 1.3 - Current version, handle both emu in one script
//=
//===== Compatible With: ===================================== 
//= rAthena SVN
//= hercules SVN
//===== Description: ========================================= 
//=
//= An instance based over mobs races
//=
//===== Todo Remaining: ======================================
//
//===== Additional Comments: =================================
//
//
//============================================================

prontera,140,170,6	script	Nest Guardian::alanegua	4_MAN_GALTUN,{

OnStart:
	.@party_id = getcharid(1);
	.@is_leader = ( getcharid(0) == getpartyleader(.@party_id,2) );
	.@has_instance = BN_INSTANCE_ID > 0;
	
	.@bnest_instance_delay = ( instance_delay_bnest - gettimetick(2) );

	mes "^0055FF[ "+.instance_name$+" ]^000000";
	mes "We're now recruiting people to Boss Nest and eliminate all the monsters that live there. Could you lend us a hand on this ?";
	mes " ";
	next; 
	mes "^0055FF[ "+.instance_name$+" ]^000000";
	mes "The Nest is a strange place, where spicies coexist with difficulties.";
	mes "Each area is specific to a specie, and it's members attack everything which doesn't belong to it.";
	mes "I don't really know why this beast grouped here.";
	mes "Maybe they're protecting something valuable?";
	next;
	switch( select( 
		( .@party_id && .@is_leader && !.@has_instance )?"Start The Mission":"",
		( .@party_id && .@has_instance )?"Enter Mission":"",
		( .@party_id && .@is_leader && .@has_instance )?"Destroy The Mission":"",
	"Leave"
		)){
	case 1:
		if( .@bnest_instance_delay > 0 )
		{
			mes "You have to wait : ^FF0000"+.@bnest_instance_delay+" Seconds.^000000";
			close;
		}
		getpartymember .@party_id,2;
		if( .min_party_member >= 1 ){
			.@origin = getcharid(3);
			.@gettimetick = gettimetick(2);
			for( .@i = 0; .@i < $@partymembercount; .@i++ )
				if( attachrid( $@partymemberaid[.@i] ) ){
					.@member_count++;
					if( BaseLevel < .base_level[0] || BaseLevel > .base_level[1] ) .@fail = .@fail|1;
					if( Zeny < .instance_zeny ) .@fail = .@fail|2;
					if( instance_delay_bnest > .@gettimetick ) .@fail = .@fail|4;
					if( .@fail ){
						.@name$ = strcharinfo(0);
						break;
					}
				}
			attachrid( .@origin );
		}
		if( .@name$ != "" && .@fail ){
			mes "^FF0000[ Failed ]^000000";
			mes "^0055FF"+.@name$+"^000000, You cannot join right now.";
			mes " ";
			mes "^FF0000[ Reason: ]^000000";
			if( .@fail & 1 ) mes "^777777You must be Level: "+.base_level[0]+" - "+.base_level[1]+"^000000";
			if( .@fail & 2 ) mes "^777777You need "+ .instance_zeny +" Zeny^000000";
			if( .@fail & 4 ) mes "^777777You still have cooldown.^000000";
			break;
		}
		if( !instance_check_party( .@party_id,.min_party_member ) ){
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "Make or join a party with at least "+.min_party_member+" member and try again.";
			break;
		}
		else{
			$bn_instance_level = select( 
				"^FF0000[ Cooldown - 4 Hours ]^000000 Easy Mode",
				"^FF0000[ Cooldown - 6 Hours ]^000000 Difficult Mode",
				"^FF0000[ Cooldown - 8 Hours ]^000000 Extreme Mode"
			);
			//HERCULES
			//.@instance = instance_create( .instance_name$,.@party_id, IOT_PARTY );
			//RATHENA
			.@instance = instance_create( .instance_name$ );
			if( .@instance < 0 ) {
				npctalk "Boss Nest reservation failed.";
				mes "^0000FF"+.instance_name$+" ^000000- Reservation Failed!";
				close;
			}
			mes "^0000FF"+.instance_name$+" ^000000 - Reserved";
			//HERCULES
			//for( .@i = 0; .@i < .map_size; .@i++ )
			//	if( instance_attachmap( .map$[.@i],.@instance,1,"BossNest" ) == "" ){
			//		npctalk "Boss Nest reservation failed.";
			//		mes "- ^FF0000Reservation Failed!^000000";
			//		instance_destroy( .@instance );
			//		close;
			//	}
			.@gettimetick = gettimetick(2);
			getpartymember .@party_id,2;
			for( .@i = 0; .@i < $@partymembercount; .@i++ )
				if( attachrid( $@partymemberaid[.@i] ) ){
					instance_delay_bnest = ( .@gettimetick + ( .instance_delay * .@level ));
					Zeny -= .instance_zeny;
					dispbottom "["+.instance_name$+"] -"+ .instance_zeny +" Zeny and Instance Cooldown Applied.";
				}
			attachrid( .@origin );
			//npctalk "Boss Nest has been generated for Party - "+getpartyname( .@party_id );
			//HERCULES
			//instance_set_timeout $time_bn_limit[0],$time_bn_limit[1],.@instance;
			//instance_attach( .@instance );
			//instance_init( .@instance );
			
			//RATHENA
			//init unused npc
			next;
		}
	case 2:
		//HERCULES
		//if( has_instance( .map$[0] ) == "" ){
		//	mes .instance_name$ + " doesnt exist for Party - "+getpartyname( .@party_id );
		//}
		//else{
		//	warp .map$[0],23,155;
		//	instance_announce $bnest_instance, getpartyname(getcharid(1))+"  entered in the "+.instance_name$+"!",bc_map,"0x00ff99";
		//}
		
		//RATHENA
		getmapxy(.@map$, .@x, .@y, UNITTYPE_PC);
		save .@map$, .@x, .@y;
		switch(instance_enter(.instance_name$)) {
			default:
				mes "An unknown error has occurred.";
				close;
			case 2:
				mes "The memorial dungeon " + .instance_name$ + " does not exist.";
				mes "The party leader did not generate the dungeon yet.";
				close;
			case 1:
				mes "You can enter the dungeon after making the party.";
				close;
			case 0:
				instance_announce $bnest_instance, strcharinfo(0)+" from party, " +getpartyname( .@party_id )+", is entering the dungeon, "+.instance_name$+".",bc_map,"0x00ff99",FW_NORMAL,12;
				end;
		}
		break;
	case 3:
		instance_destroy( BN_INSTANCE_ID );
		mes "" + .instance_name$ + " Destroyed.";
		getpartymember getcharid(1),2;
		for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
			if( attachrid( $@partymemberaid[.@i] ) && BN_INSTANCE_ID > 0){
				set BN_INSTANCE_ID, -1;
			}
		break;
	default: 
		break;
}
close;

OnInit:
	setarray .base_level,90,250;
	setarray $time_bn_limit,7200,300;
	setarray .map$,"1@bnest";
	.map_size = getarraysize( .map$ );
	.min_party_member = 1;
	.instance_zeny = 100000;
	.instance_delay = 7200;
	.instance_name$ = "Boss Nest";
	end;
}

1@bnest,23,155,4	script	boss_nest	HIDDEN_WARP_NPC,2,2,{
function summon_normal;
function summon_guardian;
function floor_reward;
end;

OnTouch:
	end;

OnInstanceInit:
	.@map$ = instance_mapname( "1@bnest", instance_id() );
	.@npc_name$ = instance_npcname( strnpcinfo(0) );
	//HERCULES
	//setmapflag .@map$,mf_zone,"Memorial Dungeon";
	'bn_instance_floor = 0;
	'bn_floor_reward = 30;
	'bn_final_reward = 50;
	summon_normal( .@map$,.@npc_name$+"::OnNormalDied" );
	
	//RATHENA
	initnpctimer;
	end;
	
OnTimer1000:
	set .countTimerRound, .countTimerRound + 1;	
	//RATHENA delay
	if (.countTimerRound >= $time_bn_limit[0])
	{
		stopnpctimer;
		instance_announce $bnest_instance,"[ Boss Nest ] : The dimension is collapsing. You should get out now!!",bc_map,0x00FF99;
		instance_destroy( $bnest_instance );
		set $instance_bnest_id, -1;
	}
	end;

function	summon_normal	{
	.@map$ = getarg(0);
	.@label$ = getarg(1);
	
	'bn_instance_floor++;
	killmonsterall .@map$;
	switch( 'bn_instance_floor ){
		case 1:
			'bn_instance_floor_name$ = "Insect Nest";
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1305,1300,1293,1321,1289,1618,1294,1194,1408,1281;
			setarray .@amount,3,3,3,3,3,3,3,3,3,3;
			break;
		case 2:
			'bn_instance_floor_name$ = "Undead Nest";
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1297,1983,1504,1117,1506,1512,1446,1132,1554,1865;
			setarray .@amount,3,3,3,3,3,3,3,3,3,3;
			break;
		case 3:
			'bn_instance_floor_name$ = "Angel Nest";
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1388,1371,1700,1702,1701,1755,1754,1703,1765;
			setarray .@amount,3,3,3,3,3,3,3,3,3;
			break;
		case 4:
			'bn_instance_floor_name$ = "Demon Nest";
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1739,1740,1301,1307,1302,1632,1773,1634;
			setarray .@amount,3,3,3,3,3,3,3,3;
			break;
		case 5:
			'bn_instance_floor_name$ = "Brute Nest";
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1283,1513,1319,1515,1387,1381,1259,1311,1310;
			setarray .@amount,3,3,3,3,3,3,3,3,3;
			break;
		case 6:
			'bn_instance_floor_name$ = "Formless Nest";
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1268,1671,1833,1774,1207,1270,1698,1366;
			setarray .@amount,3,3,3,3,3,3,3,3;
			break;
		case 7:
			'bn_instance_floor_name$ = "Humanoid Nest";
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1769,1098,1507,1830,1219,1982,1202,1829,1390;
			setarray .@amount,3,3,3,3,3,3,3,3,3;
			break;
		default:
			mapannounce .@map$, "[ Boss Nest ] : All Boss Nest has been cleared, congratulation. Boss nest close in 15 seconds.",bc_map,"0x00FF99";
			sleep 15000;
			instance_destroy();
			set $instance_bnest_id, -1;
			return;
	}
	//for debug
	//setarray .@coordinate,149,162,149,162;
	set .@map$, instance_mapname("1@bnest", instance_id());
	.@monster_size = getarraysize( .@monster );
	for( .@i = 0; .@i < .@monster_size; .@i++ ){
		.@target_amount = ( $bn_instance_level * .@amount[.@i] );
		areamonster .@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@label$;
	}
	return;
}

function	summon_guardian	{
	.@map$ = getarg(0);
	.@label$ = getarg(1);
	
	killmonsterall .@map$;
	switch( 'bn_instance_floor ){
		case 1:
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1059,1147;
			setarray .@amount,1,1;
			break;
		case 2:
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1112,1038;
			setarray .@amount,1,1;
			break;
		case 3:
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1751;
			setarray .@amount,1;
			break;
		case 4:
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1708;
			setarray .@amount,1;
			break;
		case 5:
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1779;
			setarray .@amount,1;
			break;
		case 6:
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1768;
			setarray .@amount,1;
			break;
		case 7:
			setarray .@coordinate,234,79,70,222;
			setarray .@monster,1647;
			setarray .@amount,1;
			break;
		default: 
			mapannounce .@map$,"[ Boss Nest ] : Cleared!! Congratulation!!",bc_map,0x00FF99;
			floor_reward ( getcharid(1), 'bn_final_reward );
			set #BOSSNEST, #BOSSNEST + 1;
			return;
	}
	//for debug
	//setarray .@coordinate,149,162,149,162;
	.@monster_size = getarraysize( .@monster );
	for( .@i = 0; .@i < .@monster_size; .@i++ ){
		.@target_amount = ( $bn_instance_level * .@amount[.@i] );
		areamonster .@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@label$;
	}
	return;
}

function	floor_reward	{
	.@party_id = getarg(0);
	.@arg_count = getargcount() - 1;
	.@reward = getarg(1);
	if( .@party_id ){
		getpartymember .@party_id,2;
		for( .@pt = 0; .@pt < $@partymembercount; .@pt++ ){
			if( attachrid( $@partymemberaid[.@pt] ) ){
				callfunc("AddPoints", getcharid(3), .@reward * 'bn_instance_floor * $bn_instance_level);
				dispbottom "[ Boss Nest ] : You have gained instance points for defeating Guardian of "+'bn_instance_floor_name$+".";
				logmes "[BossNest] " + strcharinfo(0) + " complete the instance and obtains " + (.@reward * 'bn_instance_floor * $bn_instance_level) + " points.";
			}
		}
		detachrid;
	}
	return;
}

OnNormalDied:
	.@map$ = strnpcinfo(4);
	.@npc_name$ = instance_npcname( strnpcinfo(0) );
	.@mob_dead_num = mobcount( .@map$,.@npc_name$+"::OnNormalDied" );
	if( !.@mob_dead_num ){
		detachrid;
		sleep 3000;
		mapannounce .@map$,"[ Boss Nest ] : Guardian of '"+'bn_instance_floor_name$+"' has been awakened!!",bc_map,0x00FF99;
		sleep 3000;
		summon_guardian( .@map$,.@npc_name$+"::OnGuardianDied" );
	}
	else if( .@mob_dead_num < 5 || .@mob_dead_num % 5 == 0 ){
		mapannounce .@map$,"[ Boss Nest ] : "+'bn_instance_floor_name$+" : "+.@mob_dead_num+" left remaining.",bc_map,0x00FF99;
	}
	end;
	
OnGuardianDied:
	.@map$ = strnpcinfo(4);
	.@npc_name$ = instance_npcname( strnpcinfo(0) );
	.@mob_dead_num = mobcount( .@map$,.@npc_name$+"::OnGuardianDied" );
	.@party_id = getcharid(1);
	
	if( !.@mob_dead_num ){
		sleep 3000;
		mapannounce .@map$,"[ Boss Nest ] : Guardian of '"+'bn_instance_floor_name$+"' has been killed.",bc_map,0x00FF99;
		floor_reward( .@party_id, 'bn_floor_reward );
		sleep 5000;
		summon_normal( .@map$,.@npc_name$+"::OnNormalDied" );
	}
	else if( .@mob_dead_num < 5 || .@mob_dead_num % 5 == 0 ){
		mapannounce .@map$,"[ Boss Nest ] : "+'bn_instance_floor_name$+" Guardian : "+.@mob_dead_num+" left",bc_map,0x00FF99;
	}
	end;
}
